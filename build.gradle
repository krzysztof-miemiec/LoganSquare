buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'com.bluelinelabs'
    version = '1.3.7-plus'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    repositories {
        jcenter()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                artifactId project.ext.artifactId

                pom.withXml {
                    def node = asNode()
                    node.appendNode('name', 'LoganSquare')
                    node.appendNode('description', 'Screaming fast JSON parsing and serialization library for Android.')
                    node.appendNode('url', 'https://github.com/bluelinelabs/LoganSquare')

                    def scmNode = node.appendNode('scm')
                    scmNode.appendNode('connection', 'scm:git:https://github.com/rs411/LoganSquare')
                    scmNode.appendNode('developerConnection', 'scm:git:https://github.com/rs411/LoganSquare')
                    scmNode.appendNode('url', 'https://github.com/rs411/LoganSquare')

                    def developersNode = node.appendNode('developers')
                    def ericNode = developersNode.appendNode('developer')
                    ericNode.appendNode('id', 'rs411')
                    ericNode.appendNode('name', 'Krzysztof Miemiec')

                    def licensesNode = node.appendNode('licenses')
                    def licenseNode = licensesNode.appendNode('license')
                    licenseNode.appendNode('name', 'Apache 2.0')
                    licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintray_username') ? bintray_username : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''
        publications = ['mavenJava']

        dryRun = false
        publish = false

        pkg {
            repo = 'maven'
            name = 'LoganSquare'
            desc = 'Screaming fast JSON parsing and serialization library for Android.'
            websiteUrl = 'https://github.com/rs411/LoganSquare'
            issueTrackerUrl = 'https://github.com/rs411/LoganSquare/issues'
            vcsUrl = 'https://github.com/rs411/LoganSquare'
            licenses = ['Apache-2.0']
            labels = ['json', 'android', 'parser', 'serializer', 'jackson']
            publicDownloadNumbers = true
            version {
                name = project.version
                gpg {
                    sign = false
                    passphrase = project.hasProperty('bintray_gpg_passphrase') ? bintray_gpg_passphrase : ''
                }
                mavenCentralSync {
                    sync = false
                    user = project.hasProperty('maven_central_username') ? maven_central_username : ''
                    password = project.hasProperty('maven_central_password') ? maven_central_password : ''
                    close = '1'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
